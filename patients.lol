BTW TO RUN THIS:                 python3 lolpython.py patients.lol
WTF CAN I HAZ A PYTHON BURGER?   python3 lolpython.py --convert patients.lol

GIMME json

FILE CAN HAZ open WIT "humanetix_data.json"!
DATA CAN HAZ json OWN load WIT FILE OK

SO IM LIKE MAIN THING?
    PRINT_PATIENTS THEZ!
    PRINT_NURSES THEZ!
    PRINT_DOCTORS THEZ!


SO IM LIKE PRINT_PATIENTS THING ?
    BTW HAD TO HACK DA OLD CODEZ TO SUPPORT \n
    VISIBLE "\\nPatients" !
    RECORDS CAN HAZ FILTER_BY_CLASS WIT "Patient" OK
    RECORDS OWN sort WIT key CAN HAZ NAME_SORTER!
    PRINT_NAMES WIT RECORDS !

SO IM LIKE PRINT_NURSES THING?
    VISIBLE "\\nNurses" !
    RECORDS CAN HAZ FILTER_BY_CLASS WIT "Nurse" OK
    RECORDS OWN sort WIT key CAN HAZ NAME_SORTER !

    NURSES_BY_WARD CAN HAZ GROUP_BY_WARD WIT RECORDS !
    WARDS CAN HAZ sorted WIT NURSES_BY_WARD OWN keys THEZ OK!

    GIMME EACH WARD IN UR WARDS ?
        VISIBLE "[Ward {0}]" OWN format WIT WARD OK!
        NURSES CAN HAZ NURSES_BY_WARD LOOK AT WARD OK
        PRINT_NAMES WIT NURSES !


SO IM LIKE PRINT_DOCTORS THING?
    VISIBLE "\\nDoctors" !
    RECORDS CAN HAZ FILTER_BY_CLASS WIT "Doctor" OK
    RECORDS OWN sort WIT key CAN HAZ NAME_SORTER!

    GIMME EACH DOCTOR IN UR RECORDS ?
        NAME CAN HAZ FORMAT_NAME WIT DOCTOR!
        PAGR CAN HAZ DOCTOR LOOK AT "pager_number"!
        VISIBLE "{0}: {1}" OWN format WIT NAME AND PAGR OK!


SO IM LIKE GROUP_BY_WARD WIT RECORDS OK?
    GROUPED_WARDS CAN HAZ INVISIBLE BUCKET
    GIMME EACH RECORD IN UR RECORDS?
        WARD CAN HAZ RECORD LOOK AT "ward_number"!

        IZ WARD AINT IN UR GROUPED_WARDS?
            GROUPED_WARDS SOME WARD OK CAN HAZ SOME!

        GROUPED_WARDS LOOK AT WARD OK OWN append WIT RECORD!

    U TAKE GROUPED_WARDS


SO IM LIKE FORMAT_NAME WIT PERSON OK?
    FIRST_NAME CAN HAZ PERSON LOOK AT "first_name"!
    LAST_NAME CAN HAZ PERSON LOOK AT "family_name"!
    U TAKE "  {0} {1}" OWN format WIT FIRST_NAME AND LAST_NAME OK


SO IM LIKE PRINT_NAMES WIT PEOPLE OK?
    GIMME EACH PERSON IN UR PEOPLE?
        VISIBLE FORMAT_NAME WIT PERSON OK!


SO IM LIKE FILTER_BY_CLASS WIT CLAZZ OK?
    U TAKE SOME PERSON GIMME EACH PERSON IN THE DATA IZ PERSON LOOK AT "class"! KINDA LIKE CLAZZ OK


SO IM LIKE NAME_SORTER WIT PERSON OK?
    U TAKE PERSON LOOK AT "family_name" OK AND PERSON LOOK AT "first_name"!


IZ __name__ KINDA LIKE "__main__"?
    MAIN THEZ!
